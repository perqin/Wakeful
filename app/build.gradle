apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

// load in properties from keystore credentials file (not in git)
apply from: '../gradle/keystore-plugin.gradle'

def gitTag = 'git tag'.execute([], project.rootDir).text.trim().split("\n").last()
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

ext {
    BUILD_TOOLS = "27.0.3"
}

android {
    compileSdkVersion 27
    buildToolsVersion BUILD_TOOLS

    defaultConfig {
        applicationId "com.gedoor.wakeful"
        minSdkVersion 24
        targetSdkVersion 27
        versionCode 10000 + gitCommits
        versionName gitTag
        project.ext.set("archivesBaseName", "Wakeful_by_kunfei_" + gitTag)
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions.incremental = false // for kotlin
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.2.20-eap-33'
    implementation 'com.android.support:support-v4:27.0.2'
}

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.20-eap-33"
    }
}
repositories {
    mavenCentral()
}
