apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

// load in properties from keystore credentials file (not in git)
apply from: '../gradle/keystore-plugin.gradle'

def gitTag = 'git tag'.execute([], project.rootDir).text.trim().split("\n").last()
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

ext {
  SUPPORT_LIBRARY_VERSION = '25.0.1'
  BUILD_TOOLS = "25.0.0"
}

android {
  compileSdkVersion 25
  buildToolsVersion BUILD_TOOLS

  signingConfigs {
    wakeful {
      storeFile file(KEYSTORE_PATH)
      storePassword KEYSTORE_PASSWORD
      keyAlias 'wakeful'
      keyPassword KEY_PASSWORD
    }
  }

  defaultConfig {
    applicationId "com.bulwinkel.wakeful"
    minSdkVersion 24
    targetSdkVersion 24
    versionCode 10000 + gitCommits
    versionName gitTag
    project.ext.set("archivesBaseName", "Wakeful_by_kunfei_" + gitTag);
  }
  buildTypes {
    debug {
      applicationIdSuffix '.debug'
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig = signingConfigs.wakeful
    }
  }
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }

  compileOptions.incremental = false // for kotlin
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  testCompile 'junit:junit:4.12'
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

buildscript {
  ext.kotlin_version = '1.1.3-2'
  repositories {
    mavenCentral()
    maven { url "https://dl.bintray.com/kotlin/kotlin-eap"}
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}
repositories {
  mavenCentral()
}
